buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath group: 'org.kravemir.lightvalue', name: 'lightvalue-gradle-plugin', version: '0.8.1'
    }
}

import groovy.swing.SwingBuilder

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.allTasks.any({ t -> t.name.contains('Sonatype') })) {
        String password = null

        if (System.console() == null) {
            new SwingBuilder().edt {
                dialog(modal: true,
                        title: 'Enter password',
                        alwaysOnTop: true,
                        resizable: false,
                        locationRelativeTo: null,
                        pack: true,
                        show: true
                ) {
                    vbox {
                        label(text: "Please enter key passphrase:")
                        input = passwordField()
                        button(defaultButton: true, text: 'OK', actionPerformed: {
                            password = new String(input.password)
                            dispose();
                        })
                    }
                }
            }
        } else {
            def passwordRaw = System.console().readPassword("\nPlease enter key passphrase: ")
            password = new String(passwordRaw)
        }

        rootProject.subprojects { project ->
            signing {
                useGpgCmd()
            }

            project.publishing.repositories.each { repo ->
                repo.credentials.password = password
            }
        }
    }
}

subprojects { subproject ->

    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'signing'
    apply plugin: 'idea'

    apply plugin: 'org.kravemir.lightvalue'

    sourceCompatibility = 1.8

    group 'org.kravemir.svg.labels'
    version rootProject.version

    ext {
        sonatypeUsername = project.hasProperty('sonatypeUsername') ? project.getProperty('sonatypeUsername') : ''
    }

    repositories {
        mavenCentral()
    }

    lightvalue.main.generateJacksonMixIns = true

    task sourceJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    tasks.withType(Jar) {
        from(rootProject.projectDir) {
            include 'LICENSE'
            into 'META-INF'
        }

        manifest {
            attributes (
                    'Implementation-Title': "${subproject.name}",
                    'Implementation-Version': version
            )
        }
    }

    artifacts {
        archives jar
        archives sourceJar
        archives javadocJar
    }

    signing {
        sign configurations.archives
        required { gradle.taskGraph.hasTask("publishMavenPublicationToSonatypeSnapshotsRepository") }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                groupId subproject.group
                version project.version

                from components.java
                artifact sourceJar
                artifact javadocJar

                pom.packaging = 'jar'
                pom.withXml {
                    asNode().with {
                        appendNode('packaging', 'jar')

                        appendNode('name', "${subproject.group}:${subproject.name}")
                        appendNode('description', 'Print labels generator from SVG templates and JSON/CSV instances data')
                        appendNode('url', 'https://gitlab.com/kravemir/lablie')

                        appendNode('licenses').with {
                            appendNode('license').with {
                                appendNode('name', 'Apache License, Version 2.0')
                                appendNode('url', 'https://www.apache.org/licenses/LICENSE-2.0.txt')
                            }
                        }

                        appendNode('developers').with {
                            appendNode('developer').with {
                                appendNode('name', 'Miroslav Kravec')
                                appendNode('email', 'kravec.miroslav@gmail.com')
                                appendNode('organization', null)
                            }
                        }

                        appendNode('scm').with {
                            appendNode('connection', 'scm:git:https://gitlab.com/kravemir/lablie.git')
                            appendNode('developerConnection', 'scm:git:ssh://git@gitlab.com:kravemir/lablie.git')
                            appendNode('url', 'https://gitlab.com/kravemir/lablie/tree/master')
                        }
                    }
                }

                pom.withXml {
                    def pomFile = file("${project.buildDir}/generated-pom.xml")
                    writeTo(pomFile)
                    def pomAscFile = signing.sign(pomFile).signatureFiles[0]
                    artifact(pomAscFile) {
                        classifier = null
                        extension = 'pom.asc'
                    }
                }

                project.tasks.signArchives.signatureFiles.each {
                    artifact(it) {
                        def matcher = it.file =~ /-(sources|javadoc|executable)\.jar\.asc$/
                        if (matcher.find()) {
                            classifier = matcher.group(1)
                        } else {
                            classifier = null
                        }
                        extension = 'jar.asc'
                    }
                }
            }
        }
        repositories {
            maven {
                name "sonatypeSnapshots"
                url 'https://oss.sonatype.org/content/repositories/snapshots'
                credentials {
                    username project.sonatypeUsername
                    password "-- not specified --"
                }
            }
            maven {
                name "sonatypeStaging"
                url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
                credentials {
                    username project.sonatypeUsername
                    password "-- not specified --"
                }
            }
        }
        idea {
            module {
                downloadJavadoc = true
                downloadSources = true
            }
        }
    }

    model {
        tasks.generatePomFileForMavenPublication {
            destination = file("$buildDir/generated-pom.xml")
        }
        tasks.publishMavenPublicationToSonatypeSnapshotsRepository {
            dependsOn project.tasks.signArchives
        }
    }

    javadoc {
        source = sourceSets.main.allJava

        // JavaFX plugin adds dependencies to implementation configuration
        classpath = sourceSets.main.compileClasspath
    }
}
